apply plugin: 'com.android.application'

android {
    compileSdkVersion compileSdk

    defaultConfig {
        applicationId "com.sample.yl.sampledemo"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //将svg图片生成指定的png图片
        //vectorDrawables.generatedDensities('xhdpi', 'xxxhdpi')
        vectorDrawables.useSupportLibrary = true
        //只保留指定和默认的资源
        resConfigs('zh-rCN', 'ko')
        //仅保留xxhdpi 图片资源（目前主流分辨率1920*1080）
        addResourceConfiguration('xxhdpi')

        //配置so库，CPU架构(真机：arm，模拟器：x86)
        //将指定的so库打包到apk内
        ndk {
            abiFilters('armeabi', 'armeabi-v7a', 'x86')
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            //为true时代表源代码混淆开启
            minifyEnabled true
            //启动资源压缩
            shrinkResources false
            //zipAlign 优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(path: ':mylibrary')

    implementation 'com.github.pinguo-zhouwei:MZBannerView:v2.0.0'    //轮播广告图效果
    implementation 'com.jzxiang.pickerview:TimePickerDialog:1.0.1'    //时间选择器dialog
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'              //沉浸式状态栏和沉浸式导航栏
    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'        //侧滑返回
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.2.3'  //图片选择库
    implementation 'com.hjq:toast:2.5'      //吐司工具类
    implementation 'com.daasuu:BubbleLayout:1.2.0'  //气泡布局
    implementation 'com.airbnb.android:lottie:2.7.0'//加载json动画库
    implementation 'com.github.getActivity:ShapeView:8.5'//解决写资源文件

    //注入组件ID及点击事件
    implementation 'com.jakewharton:butterknife:10.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'

    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
